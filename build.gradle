buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "gradle.plugin.com.github.spotbugs:spotbugs-gradle-plugin:1.6.8"
    }
}
ext.platform = "win"


apply plugin: 'idea'
idea {
    project {
        languageLevel = JavaVersion.VERSION_11
        targetBytecodeVersion = JavaVersion.VERSION_11
        vcs = 'Git'
    }
}
allprojects {
    apply plugin: 'idea'
    idea.module.outputDir file("out/production/classes") // //https://intellij-support.jetbrains.com/hc/en-us/community/posts/360000430279-Can-t-access-resource-with-Java-10?page=1#community_comment_360000102619
}



wrapper {
    gradleVersion = '5.4'
    distributionType = Wrapper.DistributionType.ALL
}

subprojects {
    apply plugin: "com.github.spotbugs"
    apply plugin: 'signing'
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'

    if (!'benchmark'.equals(project.name)){//jmh does not work with module-info
        compileJava {
            inputs.property("moduleName", "io.github.factoryfx.$project.name")
            doFirst {
                options.compilerArgs = [
                        '--module-path', classpath.asPath,
                ]
                classpath = files()
            }
        }  
    }


    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    javadoc {
        failOnError = false
        doFirst {
            exclude "**/module-info.java"
            options.addStringOption('-module-path', classpath.asPath)
        }
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives javadocJar, sourcesJar
    }

    group = 'io.github.factoryfx'
    version = '2.0.8'

    publishing {

        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifact sourcesJar
                artifact javadocJar
                pom {
                    name = 'factoryfx'
                    packaging 'jar'
                    description = 'factoryfx dependency injection framework'
                    url = 'http://factoryfx.github.io/factoryfx'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id 'SCOOP Software'
                            name 'SCOOP Software'
                        }
                    }
                    scm {
                        connection 'scm:git@github.com:factoryfx/factoryfx.git'
                        developerConnection 'scm:git@github.com:factoryfx/factoryfx.git'
                        url 'scm:git@github.com:factoryfx/factoryfx.git'
                    }
                }
            }
        }

        repositories {
            maven {
                credentials {
                    username System.getProperty("ossrhUsername")
                    password System.getProperty("ossrhPassword")
                }
                url "https://oss.sonatype.org/content/repositories/snapshots/"
            }
        }
    }

    if (project.hasProperty('secretKeyRingFileSourceURL')) {
        signing {
            sign publishing.publications.mavenJava
        }
        //workaround  to get key  file to the plugin

        // task to fetch the GPG key to sign the JAR archives
        // we put the key to build/secring.gpg where it will be picked up by the uploadArchives task of the nexus plugin

        task fetchSecretKeyRingFile {
            doLast {
                def fileTarget = "build/secring.gpg"
                def secretKeyRingFileSourceURL = project.getProperty("secretKeyRingFileSourceURL")
                def secretKeyRingFileSourceURLAuthUsername = project.getProperty("secretKeyRingFileSourceURLAuthUsername")
                def secretKeyRingFileSourceURLAuthPassword = project.getProperty("secretKeyRingFileSourceURLAuthPassword")
                project.setProperty("signing.secretKeyRingFile", fileTarget)
                file(fileTarget).getParentFile().mkdirs()
                ant.get(src: secretKeyRingFileSourceURL, dest: fileTarget, username: secretKeyRingFileSourceURLAuthUsername, password: secretKeyRingFileSourceURLAuthPassword)
            }
        }
        afterEvaluate {
            // always fetch the GPG key before signing archives
            tasks.signArchives.dependsOn fetchSecretKeyRingFile
        }
    }


//    uploadArchives {
//        repositories {
//            mavenDeployer {
//                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
//
//                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
//                    authentication(userName: ossrhUsername, password: ossrhPassword)
//                }
//
//                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
//                    authentication(userName: ossrhUsername, password: ossrhPassword)
//                }
//
//                pom.project {
//                    name 'factoryfx'
//                    packaging 'jar'
//                    // optionally artifactId can be defined here
//                    description 'factoryfx dependency injection framework'
//                    url 'http://factoryfx.github.io/factoryfx'
//
//                    scm {
//                        connection 'scm:git@github.com:factoryfx/factoryfx.git'
//                        developerConnection 'scm:git@github.com:factoryfx/factoryfx.git'
//                        url 'scm:git@github.com:factoryfx/factoryfx.git'
//                    }
//
//                    licenses {
//                        license {
//                            name 'The Apache License, Version 2.0'
//                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
//                        }
//                    }
//                    developers {
//                        developer {
//                            id 'SCOOP Software'
//                            name 'SCOOP Software'
//                        }
//                    }
//                }
//            }
//        }
//    }


    repositories {
        mavenCentral()
    }

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
    compileJava.options.encoding = 'UTF-8'

    dependencies {
        compile group: 'org.slf4j', name: 'slf4j-api', version: '1.8.0-beta4'
        compile (group: 'com.google.guava', name: 'guava', version: '26.0-jre'){
            exclude module: "jsr305" // module conflict: https://blog.codefx.org/java/jsr-305-java-9/, https://github.com/google/guava/issues/2960
        }
        testCompile group: 'org.mockito', name: 'mockito-core', version: '2.18.3'
        testCompile group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.4.0'
    }

    test {
        useJUnitPlatform()
    }

    spotbugs {
        sourceSets = [sourceSets.main]
//        toolVersion = "3.0.1"
        ignoreFailures = true
        excludeFilter = file("$rootProject.projectDir/findbugs/findbugs-exclude-filter.xml")
    }

}

project(':copperBridge') {
    dependencies {
        compile project(":factory")
        compile (group: 'org.copper-engine', name: 'copper-coreengine', version: '5.0.1'){
            exclude group: "javax.xml.bind"
            exclude group: "javax.activation"

        }
        compile (group: 'org.copper-engine', name: 'copper-ext', version: '5.0.1'){
            exclude group: "javax.xml.bind"
            exclude group: "javax.activation"
        }

        compile group: 'jakarta.activation', name: 'jakarta.activation-api', version: '1.2.1'
        compile group: 'jakarta.xml.bind', name: 'jakarta.xml.bind-api', version: '2.3.2'

        compile group: 'org.ow2.asm', name: 'asm', version: '7.0'
        compile group: 'org.ow2.asm', name: 'asm-tree', version: '7.0'
        compile group: 'org.yaml', name: 'snakeyaml', version: '1.23'
    }
}

project(':microserviceRestServer') {
    dependencies {
        compile project(":jettyFactory")
        compile project(":microserviceRestCommon")
        testCompile project(":testfactories")
    }
}

project(':microserviceRestClient') {
    dependencies {
        compile project(":factory")
        compile project(":microserviceRestCommon")
        compile (group: 'org.glassfish.jersey.ext', name: 'jersey-proxy-client', version: '2.28')
        compile ('org.glassfish.jersey.core:jersey-client:2.28')
        compile (group: 'com.fasterxml.jackson.jaxrs', name: 'jackson-jaxrs-json-provider', version: '2.9.8')
    }

}

project(':microserviceRestCommon') {
    dependencies {
        compile project(":factory")
        compile group: 'jakarta.ws.rs', name: 'jakarta.ws.rs-api', version: '2.1.2'
        compile group: 'jakarta.activation', name: 'jakarta.activation-api', version: '1.2.1'
        compile group: 'jakarta.xml.bind', name: 'jakarta.xml.bind-api', version: '2.3.2'
    }
}

project(':microserviceRestIntegrationTest') {
//    publishing.publications.remove(publishing.publications.mavenJava)//don't publish
    dependencies {
        compile project(":microserviceRestServer")
        compile project(":microserviceRestClient")
        compile (group: 'ch.qos.logback', name: 'logback-classic', version: '1.3.0-alpha4'){
            exclude group: 'javax.activation'
        }
        compile group: 'jakarta.activation', name: 'jakarta.activation-api', version: '1.2.1'
    }

    spotbugs {
        sourceSets = []
    }
}



project(':factory') {
    dependencies {
        compile (group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.8')
        compile (group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jdk8', version: '2.9.8')
        compile (group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: '2.9.8')
        
        testCompile project(":testfactories")
        testCompile (group: 'ch.qos.logback', name: 'logback-classic', version: '1.3.0-alpha4'){
            exclude group: 'javax.activation'
        }
        testCompile group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-yaml', version: '2.9.8'
    }
}

project(':postgresqlStorage') {

    dependencies {
        compile project(":factory")
        compile group: 'org.postgresql', name: 'postgresql', version: '42.2.2'
        testCompile 'ru.yandex.qatools.embed:postgresql-embedded:1.15'
        testCompile project(":testfactories")
    }

}

project(':oracledbStorage') {
    dependencies {
        compile project(":factory")
        //still no oracle driver in mvn central
        testCompile group: 'com.h2database', name: 'h2', version: '1.4.196'
        testCompile project(":testfactories")
    }

}

project(':jettyFactory') {
    dependencies {
        compile project(":factory")
        compile (group: 'org.eclipse.jetty', name: 'jetty-server', version: '9.4.14.v20181114')
        compile (group: 'org.eclipse.jetty', name: 'jetty-servlet', version: '9.4.14.v20181114')
        compile (group: 'org.eclipse.jetty', name: 'jetty-webapp', version: '9.4.14.v20181114')
        
        compile group: 'jakarta.xml.bind', name: 'jakarta.xml.bind-api', version: '2.3.2'

        compile (group: 'org.glassfish.jersey.core', name: 'jersey-server', version: '2.28')
        compile (group: 'com.fasterxml.jackson.jaxrs', name: 'jackson-jaxrs-json-provider', version: '2.9.8')
        compile ('org.glassfish.jersey.containers:jersey-container-servlet-core:2.28')
        compile ('org.glassfish.jersey.inject:jersey-hk2:2.28')

        compile group: 'jakarta.annotation', name: 'jakarta.annotation-api', version: '1.3.4'

        testCompile (group: 'ch.qos.logback', name: 'logback-classic', version: '1.3.0-alpha4'){
            exclude group: 'javax.activation'
        }
    }

}


project(':example') {

//    publishing.publications.remove(publishing.publications.mavenJava)//don't publish
    dependencies {
        compile project(":javafxFactoryEditing")
        compile project(":jettyFactory")
        compile project(":microserviceRestServer")
        compile project(":factory")
        compile project(":domFactoryEditing")

        compile (group: 'ch.qos.logback', name: 'logback-classic', version: '1.3.0-alpha4'){
            exclude group: 'javax.activation'
        }
        compile group: 'jakarta.activation', name: 'jakarta.activation-api', version: '1.2.1'

        testCompile group: 'org.hamcrest', name: 'hamcrest-library', version: '1.3'
    }

}


project(':javafxFactoryEditing') {
    dependencies {
        compile "org.openjfx:javafx-base:11.0.1:$platform"
        compile "org.openjfx:javafx-graphics:11.0.1:$platform"
        compile "org.openjfx:javafx-controls:11.0.1:$platform"
        compile "org.openjfx:javafx-web:11.0.1:$platform"
        compile "org.openjfx:javafx-fxml:11.0.1:$platform"
        compile "org.openjfx:javafx-media:11:$platform"

        compile group: 'org.controlsfx', name: 'controlsfx', version: '11.0.0'
        compile group: 'org.fxmisc.richtext', name: 'richtextfx', version: '0.6.10'

        compile project(":factory")
        compile project(":microserviceRestClient")

        testCompile project(":testfactories")
        testCompile (group: 'ch.qos.logback', name: 'logback-classic', version: '1.3.0-alpha4'){
            exclude group: "javax.activation"
        }
        testCompile group: 'jakarta.activation', name: 'jakarta.activation-api', version: '1.2.1'

    }

}

project(':javafxDistributionServer') {
    dependencies {
        compile project(":jettyFactory")
        compile project(":factory")
        compile group: 'org.slf4j', name: 'slf4j-api', version: '1.8.0-beta4'

        testCompile (group: 'ch.qos.logback', name: 'logback-classic', version: '1.3.0-alpha4'){
            exclude group: "javax.activation"
        }
        testCompile group: 'jakarta.activation', name: 'jakarta.activation-api', version: '1.2.1'
    }

}

project(':javafxDistributionClient') {

    dependencies {
        compile group: 'org.slf4j', name: 'slf4j-api', version: '1.8.0-beta4'
        compile (group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.8'){
            exclude group: "javax.annotation"
        }

        compile "org.openjfx:javafx-base:11.0.1:$platform"
        compile "org.openjfx:javafx-graphics:11.0.1:$platform"
        compile "org.openjfx:javafx-controls:11.0.1:$platform"
        compile "org.openjfx:javafx-web:11.0.1:$platform"
        compile "org.openjfx:javafx-fxml:11.0.1:$platform"


        compile (group: 'org.glassfish.jersey.core', name: 'jersey-client', version: '2.28')
        compile (group: 'com.fasterxml.jackson.jaxrs', name: 'jackson-jaxrs-json-provider', version: '2.9.8'){
            exclude group: "javax.annotation"
        }
        compile ('org.glassfish.jersey.media:jersey-media-json-jackson:2.28')
        compile ('org.glassfish.jersey.inject:jersey-hk2:2.28')
        compile group: 'jakarta.xml.bind', name: 'jakarta.xml.bind-api', version: '2.3.2'
    }

}

project(':javascript') {
    dependencies {
        compile project(":factory")

        compile (group: 'com.google.javascript', name: 'closure-compiler-unshaded', version: 'v20170218'){
            exclude module: "jsr305" // module conflict: https://blog.codefx.org/java/jsr-305-java-9/, https://github.com/google/guava/issues/2960
        }
    }
}

project(':testfactories') {
//    publishing.publications.remove(publishing.publications.mavenJava)//don't publish
    dependencies {
        compile project(":factory")
    }
}

project(':docu') {
//    publishing.publications.remove(publishing.publications.mavenJava)//don't publish
    dependencies {
        compile project(":factory")
        compile project(":jettyFactory")
        compile project(':postgresqlStorage')

        compile project(':microserviceRestServer')
        compile project(':microserviceRestClient')
        compile project(':initializr')
        
        compile group: 'io.dropwizard.metrics', name: 'metrics-jetty9', version: '3.2.4'
        compile 'ru.yandex.qatools.embed:postgresql-embedded:1.15'
        compile (group: 'ch.qos.logback', name: 'logback-classic', version: '1.3.0-alpha4'){
            exclude group: 'javax.activation'
        }
        compile group: 'jakarta.activation', name: 'jakarta.activation-api', version: '1.2.1'

        compile (group: 'io.swagger', name: 'swagger-jersey2-jaxrs', version: '1.5.22'){
            exclude group: 'com.fasterxml.jackson.core'
            exclude group: 'com.google.guava'
        }
        compile group: 'jakarta.xml.bind', name: 'jakarta.xml.bind-api', version: '2.3.2'

        testCompile group: 'org.hamcrest', name: 'hamcrest-library', version: '1.3'
    }

}

project(':javascriptDataEditing') {
    dependencies {
        compile project(":javascript")
        compile project(":javafxFactoryEditing")
    }
}

project(':nanoservice') {
    dependencies {
        compile project(":factory")

    }
}

project(':typescriptGenerator') {
    dependencies {
        compile project(":factory")
        testCompile project(":testfactories")
        testCompile project(":jettyFactory")
        testCompile project(":domFactoryEditing")
        testCompile (group: 'ch.qos.logback', name: 'logback-classic', version: '1.3.0-alpha4'){
            exclude group: 'javax.activation'
        }
    }

    task installNpm(type: Exec) {
        workingDir "$projectDir/src/test/ts/"

        if (org.gradle.internal.os.OperatingSystem.current().isWindows()) {
            commandLine "npm.cmd", "install"
        } else {
            commandLine "npm", "install"
        }
    }

    task typescriptTest(type: Exec) {
        dependsOn ':typescriptGenerator:installNpm'
        workingDir "$projectDir/src/test/ts/"

        if (org.gradle.internal.os.OperatingSystem.current().isWindows()) {
            commandLine "npm.cmd", "test"
        } else {
            commandLine "npm", "test"
        }
    }
}

project(':soapFactory') {

    repositories {
        mavenCentral()

        maven {
            url "http://maven.java.net/content/repositories/snapshots"
        }
    }

    dependencies {

        compile project(":factory")
        compile project(":jettyFactory")

        compile (group: 'org.eclipse.jetty', name: 'jetty-server', version: '9.4.14.v20181114')  {
            exclude group: "javax.annotation"
        }
        compile group: 'jakarta.xml.bind', name: 'jakarta.xml.bind-api', version: '2.3.2'
        compile (group: 'com.sun.istack', name: 'istack-commons-runtime', version: '3.0.7') {
            exclude group: 'javax.annotation'
        }

        compile group: 'org.glassfish.jaxb', name: 'jaxb-runtime', version: '2.3.2'

        compile (group: 'com.sun.xml.messaging.saaj', name: 'saaj-impl', version: '1.5.0') {
            exclude group: 'javax.annotation'
            exclude group: 'org.jvnet.mimepull'
            exclude group: 'javax.activation'
            exclude group: 'javax.xml.soap'
        }


        compile group: 'jakarta.xml.ws', name: 'jakarta.xml.ws-api', version: '2.3.2'
        compile group: 'jakarta.annotation', name: 'jakarta.annotation-api', version: '1.3.4'
        compile group: 'jakarta.xml.soap', name: 'jakarta.xml.soap-api', version: '1.4.1'
        compile group: 'jakarta.jws', name: 'jakarta.jws-api', version: '1.1.1'
    }
    
    project(':initializr') {

        dependencies {
            compile project(":factory")
            compile project(":jettyFactory")
            compile group: 'com.squareup', name: 'javapoet', version: '1.11.1'
        }
    }

    project(':benchmark') {
//        compileJava {
//            inputs.property("moduleName", "")
//            doFirst {
//                options.compilerArgs = [
//                        //'--module-path', classpath.asPath,
//                        '--class-path', classpath.asPath,
//                ]
//                classpath = files()
//            }
//        }

//        publishing.publications.remove(publishing.publications.mavenJava)//don't publish
        dependencies {
            compile project(":factory")
            compile project(":testfactories")

            compile group: 'org.openjdk.jmh', name: 'jmh-generator-annprocess', version: '1.21'
            compile group: 'org.openjdk.jmh', name: 'jmh-core', version: '1.21'
        }
    }

    project(':domFactoryEditing') {
        dependencies {
            compile project(":microserviceRestServer")
            compile project(":typescriptGenerator")
            testCompile (group: 'ch.qos.logback', name: 'logback-classic', version: '1.3.0-alpha4'){
                exclude group: 'javax.activation'
            }
        }
    }
    
}
